/*
 * Copyright 2010 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package solver;

dialect "java"

import org.optaplanner.core.api.score.buildin.bendable.BendableScoreHolder;
import domain.Task;
import domain.Employee;
import domain.Priority;

global BendableScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

rule "Skill requirements"
    when
        Task(employee != null, missingSkillCount > 0, $missingSkillCount : missingSkillCount)
    then
        scoreHolder.addHardConstraintMatch(kcontext, 0, - $missingSkillCount);
end

// ############################################################################
// Soft constraints
// ############################################################################

rule "URGENT priority"
    when
        Task(employee != null, priority == Priority.URGENT, $endTime : endTime)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, 0, - $endTime);
end

// Fairly distribute the workload among the employees
rule "Minimze makespan (starting with the latest ending employee first)"
    when
        Task(employee != null, nextTask == null, $endTime : endTime)
    then
        // Fairness and load balancing trick (see docs): squared to assure correctness in corner cases
        // eg. 3^2 + 3^2 + 3^2  = 27 < 4^2 + 2^2 + 3^2 = 29
        scoreHolder.addSoftConstraintMatch(kcontext, 1, - ($endTime * $endTime));
end

rule "NORMAL priority"
    when
        Task(employee != null, priority == Priority.NORMAL, $endTime : endTime)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, 2, - $endTime);
end

rule "LOW priority"
    when
        Task(employee != null, priority == Priority.LOW, $endTime : endTime)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, 3, - $endTime);
end



